# =======================================================
# Source Prezto.
# =======================================================
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# =======================================================
# Use .localrc for SUPER SECRET STUFF that you don't
# want in your public, versioned repo.
# =======================================================
if [[ -a ~/.localrc ]]
then
 source ~/.localrc
fi

# =======================================================
# Load all of our .zsh files from our .dotfile topics.
# =======================================================
typeset -U config_files
setopt extendedglob
config_files=($HOME/.dotfiles/^(zprezto.symlink|oh-my-zsh)/**/*.zsh)

# -------------------------------------------------------
# Load the path files
# -------------------------------------------------------
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# -------------------------------------------------------
# Load everything but the path and completion files
# NOTE: I removed support for the completion files in
# favor of oh-my-zsh completion support.
# -------------------------------------------------------
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

unset config_files

# =======================================================
# Make everything use VI keys
# =======================================================
set -o vi
export EDITOR=vim
export VISUAL=vim

# =======================================================
# To have paths colored instead of underlined
# =======================================================
ZSH_HIGHLIGHT_STYLES[path]='fg=white'

# =======================================================
# I use vim and don't want to have to remember where I am.
# But reverse history search freaking rocks, put that back.
# =======================================================
bindkey -M vicmd '?' history-incremental-search-backward
bindkey '^R' history-incremental-search-backward

# =======================================================
# RVM Integration
# =======================================================
[[ -s "$HOME/.rvm/scripts/rvm" ]] && source "$HOME/.rvm/scripts/rvm"  # This loads RVM into a shell session.
PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

